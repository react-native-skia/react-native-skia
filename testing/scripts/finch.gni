# Copyright 2022 The Chromium Authors
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//testing/test.gni")

template("android_wpt_finch_smoke_tests") {
  assert(defined(invoker.test_case))
  assert(defined(invoker.browser_apk))
  script_test(target_name) {
    script = "//testing/scripts/run_finch_smoke_tests_android.py"
    args = [
      "--test-case",
      invoker.test_case,
      "--browser-apk",
      rebase_path(root_build_dir, root_build_dir) + "/apks/" +
          invoker.browser_apk,
    ]

    if (defined(invoker.webview_provider_apk)) {
      args += [
        "--webview-provider-apk",
        rebase_path(root_build_dir, root_build_dir) + "/apks/" +
            invoker.webview_provider_apk,
      ]
    }

    if (defined(invoker.webview_installer_tool)) {
      args += [
        "--webview-installer-tool",
        rebase_path(invoker.webview_installer_tool, root_build_dir),
      ]
    }

    data = [
      "//build/android/",
      "//build/skia_gold_common/",
      "//testing/scripts/skia_gold_infra",
      "//testing/scripts/variations_smoke_test_data/",
      "//tools/android/",
      "$root_build_dir/pyproto/",
    ]
    data_deps = [
      "//android_webview/proto:aw_variations_seed_proto",
      "//chrome/test/chromedriver:chromedriver($host_toolchain)",
      "//third_party/blink/tools:wpt_tests_android_isolate",
      "//third_party/catapult/devil:devil",
      "//ui/base:goldctl",
    ]

    if (defined(invoker.data)) {
      data += invoker.data
    }

    if (defined(invoker.data_deps)) {
      data_deps += invoker.data_deps
    }
  }
}
