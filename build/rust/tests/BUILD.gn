# Copyright 2021 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/config/rust.gni")

group("tests") {
  # Build some minimal binaries to exercise the Rust toolchain
  # only if that toolchain is enabled in gn args.
  testonly = true

  # This should build with or without Rust, in different modes
  deps = [ "test_variable_source_set:test_variable_source_set_demo" ]

  # All the rest require Rust.
  if (toolchain_has_rust) {
    deps += [
      "test_cpp_including_rust",
      "test_mixed_source_set",
      "test_rlib_crate",
      "test_rust_source_set",
    ]
    if (build_rust_unit_tests) {
      deps += [
        "test_cpp_including_rust:test_cpp_including_rust_unittests",
        "test_mixed_source_set:test_mixed_source_set_rs_unittests",
        "test_rust_source_set:test_rust_source_set_unittests",
        "test_rust_source_set_non_standard_arrangement:foo_tests",
        "test_rust_unittests",
      ]
    }
    if (rustc_can_link) {
      deps += [
        "test_bin_crate",
        "test_rust_exe",
        "test_rust_multiple_dep_versions_exe",
      ]
    }
  }
}
