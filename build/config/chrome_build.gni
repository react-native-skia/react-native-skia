# Copyright 2015 The Chromium Authors
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

declare_args() {
  # Select the desired branding flavor. False means normal Chromium branding,
  # true means official Google Chrome branding (requires extra Google-internal
  # resources).
  is_chrome_branded = false

  # Whether to enable the Chrome for Testing (CfT) branding flavor. False means
  # to follow `is_chrome_branded`, true means to select CfT, which implies
  # `is_chrome_branded = True` with some overrides on top.
  #
  # Note: CfT is not intended to be used as a test runner to run tests in this
  # repository.
  #
  # Design doc: https://goo.gle/chrome-for-testing
  is_chrome_for_testing_branded = false

  # Set to true to enable settings for high end Android devices, typically
  # enhancing speed at the expense of resources such as binary sizes and memory.
  is_high_end_android = false
}

declare_args() {
  # Refers to the subdirectory for branding in various places including
  # chrome/app/theme.
  #
  # `branding_path_product` must not contain slashes.
  if (is_chrome_for_testing_branded) {
    branding_path_component = "google_chrome/google_chrome_for_testing"

    # CfT effectively uses the same branding as google_chrome, modulo icon assets.
    branding_path_product = "google_chrome"
  } else if (is_chrome_branded) {
    branding_path_component = "google_chrome"
    branding_path_product = "google_chrome"
  } else {
    branding_path_component = "chromium"
    branding_path_product = "chromium"
  }
}

declare_args() {
  # The path to the BRANDING file in chrome/app/theme.
  branding_file_path = "//chrome/app/theme/$branding_path_component/BRANDING"
}
